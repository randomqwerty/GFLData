()||<BIN>9</BIN><黑屏1>0,10</黑屏1><BGM>BGM_Empty</BGM>:
()<Speaker>付き人</Speaker>||<BIN>97</BIN><BGM>GF_Swim_Marnie</BGM><黑屏2>:ポランシーノさん！お待ちしておりました――+すでに専用のお席を用意してあります、ご案内いたしましょうか？
NPC_baolan(2)<Speaker>ポランシーノ</Speaker>||:……あ、いえ、自分で行くので大丈夫です。
NPC_baolan(2)<Speaker>付き人</Speaker>||:こちらは特別に残しておりましたシャンパンです。//nポランシーノ様のお気に入りの年代のものですよ。
()<Speaker></Speaker>||<SE1>AVG_20Winter_Drink_Tea</SE1>:グラスが運ばれてきた。//n中の淡い金色の液体からは、フルーツの爽やかな香りが放たれている。
NPC_baolan(2)<Speaker>ポランシーノ</Speaker>||:……ありがとうございます。
NPC_baolan(2)<Speaker>付き人</Speaker>||:気に入っていただけると幸いです。+今晩のパーティーを、どうぞお楽しみください。
NPC_baolan(2)<Speaker>ポランシーノ</Speaker>||:……はい。
()||<黑屏1>:
()<Speaker></Speaker>||<BIN>139</BIN><黑屏2><SE1>AVG_door_elecport</SE1>:慈善パーティー会場内にて――
NPC_baolan(0)<Speaker>ポランシーノ</Speaker>||:ふぅ――なんとか入れたよ！
NPC_baolan(0);MDR(4)<Speaker>MDR</Speaker><通讯框>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF>まだ門をくぐっただけなのに、緊張しすぎだって！</color>
NPC_baolan(0);NPC_baolan(4)<Speaker>ポランシーノ</Speaker><通讯框>||:<color=#00CCFF>ルイスさんの振る舞いは十分素晴らしいですよ。</color>+<color=#00CCFF>いくら私でも、この短時間で他人になりきることはできませんから。</color>
NPC_baolan(2)<Speaker>ルイス</Speaker>;NPC_baolan(4)<通讯框>||:ポランシーノ様、買いかぶりすぎですよ！！！
NPC_baolan(2);NPC_baolan(4)<Speaker>ポランシーノ</Speaker><通讯框>||:<color=#00CCFF>ポランシーノでいいですよ、友人からはそう呼ばれたいです。</color>
NPC_baolan(0)<Speaker>ルイス</Speaker>;NPC_baolan(4)<通讯框>||:ポ……ポランシーノ、ほんとに、呼び捨てでいいの？
NPC_baolan(0);NPC_baolan(4)<Speaker>ポランシーノ</Speaker><通讯框>||:<color=#00CCFF>もちろん、あなたたちはすでに、私の友人なんですから。</color>
NPC_baolan(2)<Speaker>ルイス</Speaker>;NPC_baolan(4)<通讯框>||:そんな……う、うれしすぎるよ！！！
NPC_baolan(2);Welrod(2)<Speaker>ウェルロッドMkⅡ</Speaker><通讯框>||:<color=#00CCFF>お嬢さん、今が任務中だということ、忘れないでくださいね。</color>
NPC_baolan(0)<Speaker>ルイス</Speaker>||:あ、そうだった。//n私はスターのポランシーノで、//n羽のカフスボタンをつけた男の人を探すんだ！
M200(4)<Speaker>M200</Speaker><通讯框>||:<color=#00CCFF>声に出す必要はないですよ。</color>
()<Speaker>？？？</Speaker>||<SE1>AVG_tele_disconnect</SE1>:ポランシーノ！久しぶり……
NPC_baolan(0)<Speaker>ルイス</Speaker>||:（誰だ？？？）+……久しぶり。
NPC_baolan(4)<Speaker>？？？</Speaker>||:『ウェイブ・ラングラー』すごくよかったよ。//n君に会うために、今回はわざわざここへ来たんだ。
NPC_baolan(4)<Speaker>ルイス</Speaker>||:……ハ、ハハ、それは縁があるわね。
NPC_baolan(4)<Speaker>？？？</Speaker>||:縁じゃないよ、僕が君を一方的に追いかけてきただけだ。+どうだ、外で風に当たりながら少しおしゃべりしないかい？
M200(4)<Speaker>M200</Speaker><通讯框>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF>この方もカフスボタンは羽じゃありませんね。</color>
NPC_baolan(4)<Speaker>ルイス</Speaker>||:ごめんなさい。//nちょっと今は気分が悪くて、風に当たるのはやめておくわ。
NPC_baolan(4)<Speaker>？？？</Speaker>||:それは残念だよ。
()<Speaker></Speaker>||:ルイスがどこかへ腰掛けようとしたその時、また何者かが歩み寄ってきた。//n見たところ、知り合ってしばらくは経っている雰囲気だった。
NPC-Bodyguard2(0)<Speaker>？？？</Speaker>||:君は今夜の大トリのはずだろう、どうしてもう来たんだ？
NPC_baolan(0)<Speaker>ルイス</Speaker>;NPC-Bodyguard2(0)||:……お節介ね。
NPC_baolan(0);NPC-Bodyguard2(0)<Speaker>？？？</Speaker>||:？？？+ポランシーノ？
NPC_baolan(0);NPC_baolan(0)<Speaker>ポランシーノ</Speaker><通讯框>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF>……ゴホン、こちらはウェイブ・ラングラー主演のゲインさんです。</color>
NPC_baolan(1)<Speaker>ルイス</Speaker>;NPC_baolan(0)||:嘘でしょ、映画と違いすぎるよ、こんなのファンでも分からないって！
NPC_baolan(1);NPC-Bodyguard2(0)<Speaker>ゲイン</Speaker>||:ポランシーノ？+誰と喋ってるんだ？
NPC_baolan(1);MDR(4)<Speaker>MDR</Speaker><通讯框>||:<color=#00CCFF>呼ばれてるよ！</color>
NPC_baolan(4)<Speaker>ルイス</Speaker>||<SE1>AVG_tele_disconnect</SE1>:あ、いや、なんでもないわ。//n今日は疲れたから、早めに来て、早めに帰ろうと思っただけ。
NPC_baolan(4);NPC-Bodyguard2(0)<Speaker>ゲイン</Speaker>||:そんなに飲んだのか？+慈善パーティーは大抵深夜まで続く。//n君は大事なゲストなんだから、早めに帰ることはできないと思うが……
NPC_baolan(4);NPC-Bodyguard2(0)<Speaker>ゲイン</Speaker>||:やはり酒は控えめにして、コーヒーを飲んだ方がいい。+目を覚ましておけ、或いは……他に何かするか？
NPC_baolan(0)<Speaker>ルイス</Speaker>||:……コーヒーもお酒も必要ないわ。
NPC_baolan(0);M200(4)<Speaker>M200</Speaker><通讯框>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF>へぇ、スターでもこんなこと言われるんですね。</color>
NPC_baolan(0);NPC_baolan(0)<Speaker>ポランシーノ</Speaker><通讯框>||:<color=#00CCFF>……映画の中と現実で全く異なる人は、やっぱり多いですよ。</color>+<color=#00CCFF>何と言っても、映画は芸術で、普通の人間は芸術の中には留まれませんから。</color>
NPC_baolan(0);NPC_baolan(4)<Speaker>ルイス</Speaker>;NPC_baolan(0)||:ポランシーノ、大変だったんだね！//nこんな奴の相手もしないといけないなんて！
NPC_baolan(0);Thunder50(2)<Speaker>Thunder</Speaker><通讯框>||:<color=#00CCFF>羽のカフスボタンを付けた人間を見つけるのが目的よ、集中して。</color>
NPC_baolan(0)<Speaker>ルイス</Speaker>||<SE1>AVG_tele_disconnect</SE1>:分かってるよ！
NPC-Bodyguard2(0)<Speaker>ゲイン</Speaker>||:ポランシーノ、今日はどうしたんだ、俺に全然かまってくれないじゃないか。
NPC_baolan(0)<Speaker>ルイス</Speaker>;NPC-Bodyguard2(0)||:（構うほどの興味がないからに決まってるでしょ！）+……いや、ちょっと体調がよくないだけよ。
NPC_baolan(0);NPC-Bodyguard2(0)<Speaker>ゲイン</Speaker>||:俺に会えてうれしくないのか？//n俺の顔を見れば、どんな病気でも治るんじゃないのか？
NPC_baolan(0)<Speaker>ルイス</Speaker>;NPC-Bodyguard2(0)||:……
NPC_baolan(0)<Speaker>ルイス</Speaker>;NPC-Bodyguard2(0)||:ごめんなさい、もう行くわ！
NPC_baolan(0);NPC-Bodyguard2(0)<Speaker>ゲイン</Speaker>||:待ってくれ、説明してくれよ……！
NPC_baolan(0)<Speaker>ルイス</Speaker><Shake>0.1</Shake>||<SE1>ClothingUp</SE1>:やっ……触らないで……もう！
()<Speaker></Speaker>||:ゲインに引っ張られたルイスは足を滑らせ、//n床に倒れる寸前、何者かによって身体を支えられた。
NPC_baolan(0)<Speaker>ルイス</Speaker>||:あ、ごめんなさい……
()<Speaker></Speaker>||:ルイスが急いで立ち上がると、目の前に若い男性が立っていた。
NPC_girill(0)<Speaker>？？？</Speaker>||:いえいえ……+あ！ポランシーノさんじゃないですか！
NPC_baolan(4)<Speaker>ルイス</Speaker>;NPC_girill(0)||:あ、こんにちは……
NPC_baolan(4);NPC_girill(0)<Speaker>？？？</Speaker>||:ポランシーノさん、あなたとともに//nこのパーティーに参加できて、光栄に思います。
NPC_baolan(4)<Speaker>ルイス</Speaker>;NPC_girill(0)||:……ありがとうございます。
NPC_baolan(4);NPC_girill(0)<Speaker>？？？</Speaker>||:正式なスタートまであと30分もあるのに、//nあなたほどのスターが等身大でいるとは。+ウェイブ・ラングラーの受賞はまさに当然の結果でしたね。//n友人と賭けをしようにも、皆あなたに賭けるので成立しませんでしたよ。
NPC_baolan(2)<Speaker>ルイス</Speaker>||:ウェイブ・ラングラーは本当に素晴らしかった！
Thunder50(2)<Speaker>Thunder</Speaker><通讯框>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF>その人のカフスボタン、見た！？</color>
NPC_baolan(1)<Speaker>ルイス</Speaker>||:……奴だ！
NPC_baolan(1);NPC_girill(0)<Speaker>？？？</Speaker>||:誰ですか？
NPC_baolan(2)<Speaker>ルイス</Speaker>;NPC_girill(0)||:ハハハ、いえ、その、休憩スペースに//n果物を取ってこようと思うのですけど、一緒にいかが？
NPC_baolan(2);NPC_girill(0)<Speaker>？？？</Speaker>||:え――女性のスターの方って、//nこういう時は何も食べないものだと思っていました。
NPC_baolan(2)<Speaker>ルイス</Speaker>;NPC_girill(0)||:……果物ぐらいは食べますよ。
NPC_baolan(2);NPC_girill(0)<Speaker>？？？</Speaker>||:それじゃいきますか？
NPC_baolan(4)<Speaker>ルイス</Speaker>||:いいですよ。
NPC-Bodyguard2(0)<Speaker>ゲイン</Speaker>||:おい、ポランシーノ、一緒に行くつもりなのか？//nそいつが誰だか分かっているのか？
NPC_baolan(0)<Speaker>ルイス</Speaker>||:……
Welrod(2)<Speaker>ウェルロッドMkⅡ</Speaker><通讯框>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF>ポランシーノ、この方を知っていますか？</color>
M200(4)<Speaker>M200</Speaker><通讯框>||:<color=#00CCFF>ポランシーノ？</color>
Welrod(2)<Speaker>ウェルロッドMkⅡ</Speaker><通讯框>||:<color=#00CCFF>おかしい、オフラインになりましたね……</color>
MDR(4)<Speaker>MDR</Speaker><通讯框>||:<color=#00CCFF>なんで、部屋にいるんだから落ちるわけないでしょ！？</color>
Welrod(2)<Speaker>ウェルロッドMkⅡ</Speaker><通讯框>||:<color=#00CCFF>おそらく電波が良くないのでしょう。ルイス、とりあえず慎重に相手して、状況を探ってください。私たちは廊下で待機していますから、何かあれば大きな声を出してください。</color>
NPC_baolan(1)<Speaker>ルイス</Speaker>||<SE1>AVG_tele_disconnect</SE1>:へ！？でもあたし……
NPC_girill(2)<Speaker>？？？</Speaker>||<BGM>BGM_Empty</BGM>:さあ、嬢ちゃん、もう人もいないし芝居を打たなくていいぞ。+ブツを渡してもらおうか？
NPC_baolan(0)<Speaker>ルイス</Speaker>;NPC_girill(2)||:……あなたは誰？
NPC_baolan(0);NPC_girill(2)<Speaker>ヴァレンタイン</Speaker>||:そうだな、ヴァレンタインと呼んでくれればいい。+美女に対しては基本的に上機嫌だが、それにも限界があるぞ。
NPC_baolan(4)<Speaker>ルイス</Speaker>;NPC_girill(2)||:そう？奇遇だね、あたしも――+正直言って、あなたが何を言ってるかよく分からないし。
NPC_girill(2)<Speaker>ヴァレンタイン</Speaker>||:……知らんぷりをしても、//n俺をごまかせないことは分かってるはずだよな、嬢ちゃん？
Welrod(2)<Speaker>ウェルロッドMkⅡ</Speaker><通讯框>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF>何が起きているのですか？</color>
Thunder50(2)<Speaker>Thunder</Speaker><通讯框>||:<color=#00CCFF>いやな予感がします……</color>
()<Speaker></Speaker>||<SE1>AVG_alarm</SE1><BGM>BGM_Empty</BGM>:――！
()<Speaker></Speaker>||<BGM>BGM_Danger</BGM>:警報が突如鳴り響き、休憩スペースにいた人間たちは慌て始めた。
NPC_baolan(1)<Speaker>ルイス</Speaker>||:！+何が起きたの？！
NPC_baolan(1);NPC_girill(2)<Speaker>ヴァレンタイン</Speaker>||:この音は恐らく火災報知器だ、船のどこかでトラブルが起きたのだろう……+しかしそれもお前とは関係ない、ただブツを差し出せばいいのだ……
NPC_baolan(0)<Speaker>ルイス</Speaker>||:だから、そのブツってなに？
()<Speaker>部下</Speaker>||:ボス、まずいですよ！
NPC_girill(2)<Speaker>ヴァレンタイン</Speaker>||:何を慌てている、落ち着け、こちらの女性がびっくりするだろう。
NPC_girill(2)<Speaker>部下</Speaker>||:うっ……！
NPC_baolan(0)<Speaker>ルイス</Speaker>||:……？
()<Speaker>部下</Speaker>||:申し訳ありません……//n実は、出火元があなたの部屋でして、それでこの警報が……
NPC_girill(3)<Speaker>ヴァレンタイン</Speaker>||:それじゃ俺の契約書……+いや、貴重品は無事なのか？
NPC_girill(3)<Speaker>部下</Speaker>||:いえ、火の勢いが強く……入ることもできないので、恐らくは……
NPC_girill(3)<Speaker>ヴァレンタイン</Speaker><Shake>0.1</Shake>||<SE1>Rope</SE1>:<size=55>なに！？</size>+クソが——！！！お前たちはこちらの女性を部屋へ送ってやれ、俺が見に行く――
NPC_baolan(0)<Speaker>ルイス</Speaker>||:……
()||<黑屏1>:
()<Speaker></Speaker>||<BIN>82</BIN><黑屏2><BGM>BGM_Empty</BGM>:部屋の中にて――
MDR(4)<Speaker>MDR</Speaker>||<BGM>BGM_Room</BGM>:まだ着替え終わってないの？！
Lewis(9)<Speaker>ルイス</Speaker>||<SE1>AVG_Door_Open_Close</SE1>:終わった終わった！この格好で保安人形たちを片付けるのも大変だよ！+そういや、あたしのもう一着の服は？
Welrod(2)<Speaker>ウェルロッドMkⅡ</Speaker>||:とりあえず元のを着てください。//nそれより、さっきの警報は一体なんだったのですか？
Lewis(9)<Speaker>ルイス</Speaker>||:ヴァレンタインの部屋が燃えたの、マフィアのことを聞きだす暇もなく……+ポランシーノの方は何か起きたの？
Welrod(2)<Speaker>ウェルロッドMkⅡ</Speaker>||:部屋に争った形跡はありません、//nポランシーノさんは連れ去られたわけじゃないと思います。//nですが、そうなると探す当てもありません。+それに、さっきの会話を聞くに、//nヴァレンタインは彼女を傷つけるつもりだったとは思えません……
MDR(4)<Speaker>MDR</Speaker>||:……ルイス、更衣室の彫刻はあんたが持ち帰ったの？
Lewis(9)<Speaker>ルイス</Speaker>||:彫刻？
()<Speaker></Speaker>||<SE1>ClothingUp</SE1>:ルイスは振り向き、MDRが更衣室から大げさな彫刻を//n持ってくるのを見た。それには見覚えがあるような……
Lewis(9)<Speaker>ルイス</Speaker>||:彫刻なんか持ってきてないよ、最初から更衣室にあったんじゃないの？
()<Speaker></Speaker>||:ルイスは彫刻を手に取り、じっくり眺めた。
Lewis(9)<Speaker>ルイス</Speaker>||:それにしても見たことあるなぁ、絶対どこかで……
()<Speaker></Speaker>||<SE1>AVG_doorknock_wood</SE1>:――
Lewis(7)<Speaker>ルイス</Speaker>||:へ、誰？
()<Speaker></Speaker>||<震屏3><SE1>AVG_Door_Hit</SE1><BGM>BGM_Empty</BGM>:（衝撃音）――！+ドアが無理やり破られ、十名の保安人形がなだれ込んだ。
()<Speaker>保安人形</Speaker>||:放火と窃盗の罪で逮捕します。
()||<黑点1><BIN>10</BIN>: