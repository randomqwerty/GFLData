()||<BIN>9</BIN><黑屏1>0,10</黑屏1><BGM>BGM_Empty</BGM>:
Falcon(0)<Speaker>Falcon</Speaker>||<BGM>Campaion-34</BGM><黑屏2><BIN>80</BIN>:...
()<Speaker></Speaker>||<SE1>ClothingUp</SE1>:Falcon paces back and forth outside the cafe, peering in the room every now and then.
()<Speaker></Speaker>||:At this very moment, CZ2000 and Skorpion are standing against the wall, with rubber gloves used for dishwashing on their hands. In front of them is EVO3, who just arrived at the scene after hearing about the news.
EVO3(0)<Speaker>EVO3</Speaker>||:You sneaked into the training ground, then came to the cafeteria to pull a "dine and dash"... +All this just to get yourself mentioned by Falcon in her song?
CZ2000(0)<Speaker>CZ2000</Speaker>||:...
VZ61(0)<Speaker>Skorpion</Speaker>||:...
EVO3(0)<Speaker>EVO3</Speaker>||<震屏3><SE1>AVG_Punch</SE1>:<size=50>What are you? Children?!</size>+ I told you earlier to reflect on your actions, and this is what you come up with!?
()<Speaker></Speaker>||:EVO3 shakes her head in disappointment.
EVO3(0)<Speaker>EVO3</Speaker>||:I'll pay for the dessert now. You two, write me a reflection once you get back. Am I clear?
()<Speaker></Speaker>||:With that said, EVO3 exits the cafe, letting out a deep sigh as she and Falcon exchange glances. +Following her, the now-freed duo also waddle out.
Falcon(0)<Speaker>Falcon</Speaker>||:Skorpion, CZ2000.
VZ61(0)<Speaker>Skorpion</Speaker>||:Hmm?
CZ2000(0)<Speaker>CZ2000</Speaker>||:Falcon!? +Ah-ahem, a lot just happened, ahahahaha...
()<Speaker></Speaker>||:CZ2000 chuckles as her frustration subsides.
CZ2000(0)<Speaker>CZ2000</Speaker>||:Sorry... for pestering you with a load of nonsense. We must've wasted a lot of your time. 
Falcon(0)<Speaker>Falcon</Speaker>||:Oh no, I didn't mean to blame you! +However, EVO3 said that you went through all this just to...
VZ61(0)<Speaker>Skorpion</Speaker>||:W-we were desperate after all!
Falcon(0)<Speaker>Falcon</Speaker>||:Desperate?
CZ2000(0)<Speaker>CZ2000</Speaker>||:Forget it, I'll just say it! +Didn't you say you wanted to create a song to commemorate your growth before your Neural Upgrade? +Wouldn't it be really embarrassing if we weren't even mentioned in that song as comrades who helped you on your journey?
VZ61(0)<Speaker>Skorpion</Speaker>||:So we thought about doing these things just to make you feel we contributed at least something. +We didn't expect it to turn out this way...
Falcon(0)<Speaker>Falcon</Speaker>||:...+Pfft! That's it? That's all this fuss was about?
VZ61(0)<Speaker>Skorpion</Speaker>||:Ye-yeah... W-what's wrong with that?!
Falcon(0)<Speaker>Falcon</Speaker>||:Well, it's not a matter of being right or wrong... But, you girls seem to have forgotten "the reason" why I've been able to make it this far.
CZ2000(0)<Speaker>CZ2000</Speaker>||:"The reason"...?
Falcon(0)<Speaker>Falcon</Speaker>||:Yep~!
()||<黑屏1>:
Falcon(0)<Speaker>Falcon</Speaker>||<黑屏2><BIN>9</BIN><BGM>BGM_Empty</BGM>:I've always kept it close to my heart...
()||<黑屏1>:
()<Speaker></Speaker>||<黑屏2>:...
()<Speaker>???？</Speaker>||:...Falcon...
()<Speaker>???？</Speaker>||:...Falcon...!
()||<黑屏1>:
()<Speaker></Speaker>||<黑屏2><BIN>455</BIN><BGM>GF_EV9_Story</BGM>:A long time ago, on a certain battlefield.
()<Speaker>???？</Speaker>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF>Falcon, respond if you copy! </color>
Falcon(0)<Speaker>Falcon</Speaker>||:C-copy!
()<Speaker></Speaker>||<SE1>ClothingUp</SE1>:Concealed in the shadows, Falcon answers the comms hastily. The image of CZ2000 surfaces in her vision.
CZ2000(0)<Speaker>CZ2000</Speaker><通讯框>||:<color=#00CCFF>Our target just appeared. I've sent you the coordinates.</color> +<color=#00CCFF> Can you spot it from your position?</color>
()<Speaker></Speaker>||<SE1>AVG_Camera_Focus</SE1>:After seeing CZ2000 waving at her from a distance, Falcon adjusts the angle of her rifle. A few hundred meters off the coordinates, she picks up the silhouette of a hulking autonomous weapon.
Falcon(0)<Speaker>Falcon</Speaker>||:I-I see it...!
CZ2000(0)<Speaker>CZ2000</Speaker><通讯框>||:<color=#00CCFF>Okay, Falcon, now's your time to shine~! </color>
Falcon(0)<Speaker>Falcon</Speaker>||:Yes...!
VZ61(0)<Speaker>Skorpion</Speaker><通讯框>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF>You okay, Falcon? Your voice is shaking. Are you still nervous? </color>
Falcon(0)<Speaker>Falcon</Speaker>||:Ah...
CZ2000(0)<Speaker>CZ2000</Speaker><通讯框>||:<color=#00CCFF>It's her first combat mission since joining our squad. Cut her some slack; you'd be nervous, too, if you were in her position! </color>
VZ61(0)<Speaker>Skorpion</Speaker><通讯框>||:<color=#00CCFF>Hah, if it was me, I'd charge straight in there right now and give that hunk of scrap metal a taste of my submachine guns!</color>
CZ2000(0)<Speaker>CZ2000</Speaker><通讯框>||:<color=#00CCFF>What a rash dumbo...</color> + <color=#00CCFF>Falcon, forget about her. Just focus on doing your part!</color>
Falcon(0)<Speaker>Falcon</Speaker><Delay>||<SE1>AVG_tele_disconnect</SE1>:Yes, I'll make this one count!
Falcon(0)<Speaker>Falcon</Speaker>||:...Breath... Relax...
()<Speaker></Speaker>||:However, the more pressure she puts on herself, the harder it is for Falcon to steady her hands.
()<Speaker></Speaker>||<BGM>BGM_Empty</BGM><震屏3>:Bang!
()<Speaker></Speaker>||:The bullet turns into a thin ray of light headed straight towards the target...+...
VZ61(0)<Speaker>Skorpion</Speaker><通讯框>||:<color=#00CCFF> Eh?! Looks like that guy didn't get hit? </color>
VZ61(0)<Speaker>Skorpion</Speaker>||<BGM>BGM_Danger</BGM>:<color=#00CCFF>It picked up that shot. It's scanning for us now. Get down! </color>
Falcon(0)<Speaker>Falcon</Speaker>||:Oh no...!
()<Speaker></Speaker>||:Bang!
()<Speaker></Speaker>||:In the heat of the moment, Falcon pulls the trigger once again.
CZ2000(0)<Speaker>CZ2000</Speaker><通讯框>||:<color=#00CCFF>Falcon, stop shooting. You'll expose yourself like that! </color>
Falcon(0)<Speaker>Falcon</Speaker>||:...!!
()<Speaker></Speaker>||:Just as CZ2000 warned, Falcon realizes that she's staring down her opponent's barrel.
Falcon(0)<Speaker>Falcon</Speaker>||:Cr-crap!
()<Speaker></Speaker>||:Realizing that her position is compromised, Falcon immediately thinks about repositioning. However, before she can act on that thought, a grenade has already landed next to her feet—
()<Speaker></Speaker>||<震屏3><SE1>Explode</SE1>:Boom!
()<Speaker></Speaker>||:The shockwave topples over Falcon while the impact of the explosion skews her vision.
()<Speaker></Speaker>||<SE1>AVG_BaseDoor_Open</SE1>:As she struggles to get back on her feet, the enemy has already encroached onto her position, its dark muzzle pointed at her head. +In that instant, the prey has turned into a starving wolf, and the hunter a lamb waiting to be slaughtered...
()<Speaker></Speaker>||<BIN>9</BIN><SE1>AVG_The_Division_Flash_Bomb_Explosion</SE1>:Bam!
Falcon(0)<Speaker>Falcon</Speaker><Shake>0.1</Shake>||:Eh?!
()||<黑屏1>:
()<Speaker></Speaker>||<睁眼><common_effect>%%code=AVG_chuxian_yan%%</common_effect>:Falcon opens her eyes, realizing that smoke has engulfed her surroundings.
CZ2000(0)<Speaker>CZ2000</Speaker>||:Hush—Turn off your signal!
VZ61(0)<Speaker>Skorpion</Speaker>||:This way!
()<Speaker></Speaker>||:Skorpion and CZ2000 cut through the smoke, each grabbing Falcon by one of her shoulders and drag her into a pile of rubble farther down the road.
()||<黑点1>:
()<Speaker></Speaker>||<BGM>BGM_Empty</BGM><黑点2><BIN>469</BIN>:...
()<Speaker></Speaker>||<BGM>GF_EV9_Intermission</BGM>:CZ2000 peeps through the cracks of the ruins to keep an eye on the autonomous weapon, trying to find them; she then turns around to check on Falcon's injuries.
CZ2000(0)<Speaker>CZ2000</Speaker>||:...Fortunately, no damage done is critical.
Falcon(0)<Speaker>Falcon</Speaker>||:S-sorry, I messed up...
VZ61(0)<Speaker>Skorpion</Speaker>||:Don't say that. It's obviously that guy's fault for being such a hard nut to crack!
CZ2000(0)<Speaker>CZ2000</Speaker>||:Falcon, can you keep going?
Falcon(0)<Speaker>Falcon</Speaker>||:I...
()<Speaker></Speaker>||:That explosion came so abruptly that it's only now that Falcon realizes her hands are shaking uncontrollably.
VZ61(0)<Speaker>Skorpion</Speaker>||:Oh. It's your first deployment and you already have to deal with this sort of stuff; it's totally understandable that you'd react this way.
Falcon(0)<Speaker>Falcon</Speaker>||:I'm sorry...
CZ2000(0)<Speaker>CZ2000</Speaker>||:Falcon, we can at best hold the enemy at bay. Your firepower is integral to our success.
Falcon(0)<Speaker>Falcon</Speaker>||:B-but... what if I mess it up again...
()<Speaker></Speaker>||:Thinking back to her unsuccessful shots, Falcon's confidence is shattered.
CZ2000(0)<Speaker>CZ2000</Speaker>||:So what?
Falcon(0)<Speaker>Falcon</Speaker>||:Eh?
CZ2000(0)<Speaker>CZ2000</Speaker>||:Falcon, the fight's not over yet, and we still have a chance to win. +Don't forget, the three of us are a team. You don't have to bear all the weight by yourself.
VZ61(0)<Speaker>Skorpion</Speaker>||:Exactly! If you can't solve the problem with one shot or two, just fire a dozen more~! +Don't be afraid of mistakes because you still have us by your side!
Falcon(0)<Speaker>Falcon</Speaker>||:Everyone...
Falcon(0)<Speaker>Falcon</Speaker>||<BGM>BGM_Empty</BGM>:...
()<Speaker></Speaker>||:After a brief silence, Falcon nods her head with a hint of more determination in her eyes.
Falcon(0)<Speaker>Falcon</Speaker>||:I-I want to take another shot at it! +Just that... if I miss, it'll come right at me like it did earlier...
()<Speaker></Speaker>||:Hearing Falcon's concern, CZ2000 and Skorpion exchange a glance and laugh.
()||<黑屏1>:
()<Speaker>CZ2000&Skorpion</Speaker>||<黑屏2><BIN>9</BIN><BGM>10301</BGM>:Leave that to us!
()||<黑屏1>:
()<Speaker></Speaker>||<黑屏2><BIN>207</BIN>:Outside the ruins, the autonomous weapon has yet to give up on searching for the trio. Suddenly, CZ2000 and Skorpion's signatures appear within its radar range.
VZ61(0)<Speaker>Skorpion</Speaker>||:Tsk, it already spotted us!
CZ2000(0)<Speaker>CZ2000</Speaker>||:What a pain in the ass to deal with.
VZ61(0)<Speaker>Skorpion</Speaker>||:Hmph! Come on and get it; watch me beat you into scrap!
()<Speaker></Speaker>||:The taunt seems to be effective, as the autonomous weapon immediately starts pursuing her. +Instead of confronting it directly, the two pull back and keep their distance.
CZ2000(0)<Speaker>CZ2000</Speaker>||:Falcon, the target is distracted. You can move now!
Falcon(0)<Speaker>Falcon</Speaker>||:Roger that!
()||<黑点1>:
()<Speaker></Speaker>||<黑点2><BIN>468</BIN><SE1>RunStep</SE1>:Rifle in hand, Falcon finds another vantage point.
()<Speaker></Speaker>||<SE1>AVG_Camera_Focus</SE1>:She ducks behind the partially destroyed wall and readies her crosshair at the enemy currently pursuing CZ2000 and Skorpion. Due to its constant movement, Falcon can barely maintain her lead on the target.
Falcon(0)<Speaker>Falcon</Speaker>||:...
()<Speaker></Speaker>||<震屏3><SE1>AVG_rifle_finalshot_h</SE1>:Bang!+...
VZ61(0)<Speaker>Skorpion</Speaker><通讯框>||<SE1>AVG_tele_connect</SE1>:<color=#00CCFF> Falcon, that one missed. That guy's still alive and kicking! </color>
Falcon(0)<Speaker>Falcon</Speaker>||:H-he's moving too fast! I'll try again... Ah!
()<Speaker></Speaker>||:That earlier shot did in fact alert the autonomous weapon, and Falcon sees through her scope that it's rapidly approaching her while utilizing the ruins as cover.
Falcon(0)<Speaker>Falcon</Speaker>||:Crap...!
()<Speaker></Speaker>||:The enemy hidden among cover moves swiftly, making it far more difficult for Falcon to maintain her sight on target. +Her hands couldn't help but tremble at the prospect of being overwhelmed once again.
()<Speaker>Falcon</Speaker>||:I can't... after all...
()<Speaker></Speaker>||:...
()<Speaker>???？</Speaker>||:Don't you dare go near her!!!
Falcon(0)<Speaker>Falcon</Speaker>||:Eh?
()<Speaker></Speaker>||<SE2>Gunfight</SE2><BGM>10256</BGM>:Bursts of rapid fire erupt less than a hundred meters away from her position. Falcon looks up to find two silhouettes clinging to a broken wall, holding off the advance of the autonomous weaponry head-on.
()||<黑点1>:
Falcon(0)<Speaker>Falcon</Speaker>||<黑点2><BIN>207</BIN>:CZ2000!? Skorpion!?
VZ61(0)<Speaker>Skorpion</Speaker>||:Don't be surprised. I'll show you what it takes to be a submachine gun today!
CZ2000(0)<Speaker>CZ2000</Speaker>||:I must look handsome in Falcon's eyes today.
()<Speaker></Speaker>||:The enemy, seemingly realizing that the two are, in fact, a formidable obstacle that cannot be ignored, concentrates its fire on them. +Shells mercilessly devastate whatever little cover they have left, while the two do not flinch in the slightest.
()||<黑点1>:
Falcon(0)<Speaker>Falcon</Speaker>||<黑点2><BIN>468</BIN>:...
()||<黑屏1>:
()<Speaker>CZ2000</Speaker>||<BIN>9</BIN><黑屏2>:Falcon, the fight's not over yet, and we still have a chance to win. +Don't forget, the three of us are a team. You don't have to bear all the weight by yourself.
()<Speaker>Skorpion</Speaker>||:Exactly! If you can't solve the problem with one shot or two, just fire a few more~! +Don't be afraid of mistakes...
Falcon(0)<Speaker>Falcon</Speaker>||:Because I still have comrades by my side...!+...
()<Speaker></Speaker>||:Seeing the back of her comrades, Falcon's heart is finally at peace. +She ejects the empty casing and raises the gun, then, centers the crosshairs firmly on the target.
Falcon(0)<Speaker>Falcon</Speaker>||:Leave it to me.
()<Speaker></Speaker>||<BGM>BGM_Empty</BGM><BIN>9</BIN>:Bang!
()||<黑屏1>:
()<Speaker></Speaker>||<黑屏2><BIN>9</BIN>:...
()||<黑屏1>:
()<Speaker></Speaker>||<黑屏2><BIN>80</BIN><BGM>m_avg_casual</BGM>:In the end, I mustered the courage and finally defeated that formidable enemy. +Hahahaha~ Looking back now, it feels as if it was yesterday~!
VZ61(0)<Speaker>Skorpion</Speaker>||:Now that you say it, I remember now... In retrospect, I was quite imposing!
CZ2000(0)<Speaker>CZ2000</Speaker>||:S-so you do remember my handsome moments~!
Falcon(0)<Speaker>Falcon</Speaker>||:Of course I remember!
Falcon(0)<Speaker>Falcon</Speaker>||:It's only thanks to you that I was able to defeat it! +You stood out to cover me, and that's how I got the courage to take out the enemy!
VZ61(0)<Speaker>Skorpion</Speaker>||:Thanks to...
CZ2000(0)<Speaker>CZ2000</Speaker>||:Us?
Falcon(0)<Speaker>Falcon</Speaker>||:Yes~! Ever since then, every time I think back to you facing overwhelming odds, I can't help but feel I'm still weak and need to get stronger. +So, I've been training vigorously with you as my role models, hoping that one day I can catch up to you!+I say this from the bottom of my heart, and it's something that will never change, not even by the upcoming Neural Upgrade!
CZ2000(0)<Speaker>CZ2000</Speaker>||<BGM>BGM_Empty</BGM>:...
VZ61(0)<Speaker>Skorpion</Speaker>||:...
Falcon(0)<Speaker>Falcon</Speaker>||:...+...Why don't you say something? +It got quiet all of a sudden, I'm now embarrassed....!
VZ61(0)<Speaker>Skorpion</Speaker>||:I-I don't even... I'm so mortified that I don't know what to say!!!!
Falcon(0)<Speaker>Falcon</Speaker>||:Eh?!
CZ2000(0)<Speaker>CZ2000</Speaker>||<BGM>GF_Memorial</BGM>:I didn't realize how great we are in your eyes. Yet we... Whaa!!!
Falcon(0)<Speaker>Falcon</Speaker>||:P-please don't burst into tears just because of that...
CZ2000(0)<Speaker>CZ2000</Speaker>||:It's about being the bigger person! To think, all we wanted was for you to add us into the lyrics of your song; it's too childish! +As teammates, as comrades who've been through hell together, we should have been with you all along the way.
VZ61(0)<Speaker>Skorpion</Speaker>||:Yep!
Falcon(0)<Speaker>Falcon</Speaker>||:CZ2000... Skorpion...! +Whew~ Feels great to get everything off my chest~! +From now on, let's help each other as we move forward together!
()<Speaker></Speaker>||:Falcon reaches out her right hand and looks at the two. Skorpion and CZ2000 both break into a teary smile and extend their hand to meet with Falcon.
()||<黑屏1>:
()<Speaker>三人</Speaker>||<黑屏2><BIN>9</BIN>:Fight—On—!
()||<黑点1>:
